wonderworld,173,89,4	script	แลกคอสตูมเทพ#01	11003,{ 
soundeffect "stylist.wav",0;
callfunc "qshop01a2515415"; 

OnInit:
	waitingroom	"ซื้อคอสตูมเทพ",0;
	end;
}

//============================================================
// Script Core
//============================================================

-	script	quest_shop01a545451	-1,{ 

function Add; 
function Chk; 
function Slot; 
function A_An;

OnInit:

	freeloop(1);

// ===========================================================
//  Configulator Shop
// ===========================================================

	set .Announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,300;	// Max number of quest items purchased at one time.

// ===========================================================
//  Points 
//	setarray .Points$[0],"<variable name>","<display name>";
// ===========================================================

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// ===========================================================
//  Shop Dummy Data
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>","<Shop 3>,"<Shop 4>"{,...};
// ===========================================================

	setarray .Shops$[1],"หมวก",
						"หู",
						"ปาก";							
// ===========================================================
//  Quest items
//	Add(<shop ID>,<reward ID>,<reward amount>,<Zeny cost>,<point cost>,<required item ID>,<required item amount>{,...});
// ===========================================================

//หมวก

	Add(1,64820,1,0,0,36016,1);
	Add(1,64825,1,0,0,36016,1);
	Add(1,64827,1,0,0,36016,1);
	Add(1,64831,1,0,0,36016,1);
	Add(1,64832,1,0,0,36016,1);
	Add(1,64843,1,0,0,36016,1);
	Add(1,64844,1,0,0,36016,1);
	Add(1,64845,1,0,0,36016,1);
//หู

	Add(2,64821,1,0,0,36016,1);
	Add(2,64822,1,0,0,36016,1);
	Add(2,64823,1,0,0,36016,1);
	Add(2,64824,1,0,0,36016,1);
	Add(2,64826,1,0,0,36016,1);
	Add(2,64828,1,0,0,36016,1);
	Add(2,64829,1,0,0,36016,1);
	Add(2,64830,1,0,0,36016,1);
	Add(2,64833,1,0,0,36016,1);
	Add(2,64846,1,0,0,36016,1);
//ปาก
	Add(3,64834,1,0,0,36016,1);
	Add(3,64835,1,0,0,36016,1);
	Add(3,64836,1,0,0,36016,1);
	Add(3,64837,1,0,0,36016,1);
	Add(3,64838,1,0,0,36016,1);
	Add(3,64839,1,0,0,36016,1);
	Add(3,64840,1,0,0,36016,1);
	Add(3,64841,1,0,0,36016,1);
	Add(3,64842,1,0,0,36016,1);
	Add(3,64847,1,0,0,36016,1);

// =============================================================

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop01a2515415"+.@i,909;
	}
	end;

OnMenu:

	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"เกิดข้อผิดพลาด.";
		end;
	}
	dispbottom "กรุณาเลือกไอเท็มที่ท่านต้องการ.";
	callshop "qshop01a2515415"+.@i,1;
	npcshopattach "qshop01a2515415"+.@i;
	end;

OnBuyItem:

	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "ไอเท็มที่เลือก: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "ไอเท็มที่ต้องการ:";
	disable_items;
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ แลก ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ ทดลองใส่...":"")+": ~ ^FF0000ยกเลิก^000000")) {
		case 1:
			if (@qe[0]) { 
				mes "[Quest Shop]";
				mes "คุณ... ไม่มีไอเท็มที่ต้องใช้แลกเปลี่ยน";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000กรุณา ลด น้ำหนักลงจำนวน "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" หน่วย เพื่อที่จะสำเร๊จเควสดังกล่าวได้.^000000";
				close;
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.Announce) announce strcharinfo(0)+" ได้ สำเร๊จ เควส "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:

	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {

	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop01a2515415"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {

	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {

	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {

	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "-> "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	qshop01a2515415	{

	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop01a545451::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop01a25154151	-1,909:-1
-	shop	qshop01a25154152	-1,909:-1
-	shop	qshop01a25154153	-1,909:-1
-	shop	qshop01a25154154	-1,909:-1
-	shop	qshop01a25154155	-1,909:-1
-	shop	qshop01a25154156	-1,909:-1
-	shop	qshop01a25154157	-1,909:-1
-	shop	qshop01a25154158	-1,909:-1
-	shop	qshop01a25154159	-1,909:-1
-	shop	qshop01a251541510	-1,909:-1
-	shop	qshop01a251541511	-1,909:-1
-	shop	qshop01a251541512	-1,909:-1
-	shop	qshop01a251541513	-1,909:-1
-	shop	qshop01a251541514	-1,909:-1
-	shop	qshop01a251541515	-1,909:-1
-	shop	qshop01a251541516	-1,909:-1
-	shop	qshop01a251541517	-1,909:-1
-	shop	qshop01a251541518	-1,909:-1
-	shop	qshop01a251541519	-1,909:-1
-	shop	qshop01a251541520	-1,909:-1
-	shop	qshop01a251541521	-1,909:-1
-	shop	qshop01a251541522	-1,909:-1
-	shop	qshop01a251541523	-1,909:-1
-	shop	qshop01a251541524	-1,909:-1
-	shop	qshop01a251541525	-1,909:-1
-	shop	qshop01a251541526	-1,909:-1