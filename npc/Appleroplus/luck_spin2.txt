-	script	CfgLuckSpin2	-1,{
	end;
	
OnInit:
	set $@LS_req_item2,7539;
	set $@LS_req_item2_amount,50;
	
	set $@LS_slot_default_filename2$,"spin_01";
	setarray $@LS_slot_filename2$[0],
		"spin_01", "spin_02", "spin_03", "spin_04", "spin_05", "spin_06", "spin_07", "spin_08", "spin_09", "spin_10",
		"spin_11", "spin_12", "spin_13", "spin_14", "spin_15", "spin_16", "spin_17", "spin_18", "spin_19", "spin_20",
		"spin_21", "spin_22", "spin_23", "spin_24";
		
	setarray $@LS_slot_item2[0],
		14533, 607, 7619, 61001, 7227, 601, 7620, 12424, 6235, 12216,
		12429, 6234, 12215, 12431, 12432, 12218, 12433, 12434, 63010, 616,
		12210, 12246, 6233, 12430;
		
	// system
	setarray $@LS_size_db2[0],
		getarraysize($@LS_slot_filename2$);
	end;
}

function	script	LS_NextSlot2	{
	set .@img,getarg(0) + 1;
	
	if (.@img >= $@LS_size_db2[0])
		set .@img,0;
		
	cutin $@LS_slot_filename2$[.@img], 4;
	sleep2 getarg(1);
	
	return .@img;
}

wonderworld,114,134,4	script	Luck Poring#NPC	11002,{
	set .@npc$,"["+strnpcinfo(1)+"]";
	cutin $@LS_slot_default_filename2$, 4;
	
	mes .@npc$;
	mes "ยินดีต้อนรับสู่ Luck Spin";
	next;
	if (select("- ต้องการเล่น", "- ยกเลิก") == 2) goto L2_Cancel;
	mes .@npc$;
	mes "ท่านต้องมี "+getitemname($@LS_req_item2)+" จำนวน "+$@LS_req_item2_amount;
	mes "เพื่อใช้ในการเล่น";
	next;
	if (select("- ตกลง", "- ยกเลิก") == 2) goto L2_Cancel;
	if (countitem($@LS_req_item2) < $@LS_req_item2_amount)
	{
		mes .@npc$;
		mes "ท่านมี "+getitemname($@LS_req_item2)+" ไม่พอ";
		next;
		goto L2_Cancel;
	}
	delitem $@LS_req_item2, $@LS_req_item2_amount;
	mes .@npc$;
	mes "กดปิดหน้าต่างเพื่อเริ่มเล่น";
	close2;
	pcblockmove getcharid(3),1;
	
	set .@img,0;
	set .@round,rand(5, 10);
	set .@speed,10;
	
	for (set .@i,0; .@i < .@round; set .@i,.@i+1)
	{
		set .@mini_round,rand($@LS_size_db2[0]/2, $@LS_size_db2[0]);
		for (set .@j,0; .@j < .@mini_round; set .@j,.@j+1)
			set .@img,callfunc("LS_NextSlot2", .@img, .@speed);
		set .@speed,.@speed+10;
	}

	set .@round,rand($@LS_size_db2[0]/2, $@LS_size_db2[0]);
	
	for (set .@i,0; .@i < .@round; set .@i,.@i+1)
	{
		set .@img,callfunc("LS_NextSlot2", .@img, .@speed);
		set .@speed,.@speed+50;
	}

	set .@min,($@LS_size_db2[0]/2)/2;
	set .@round,rand(.@min, $@LS_size_db2[0]/2);
	set .@round_per_50,.@round/2;
	set .@round_per_25,.@round_per_50/2;

	for (set .@i,0; .@i < .@round; set .@i,.@i+1)
	{
		set .@img,callfunc("LS_NextSlot2", .@img, .@speed);

		if (.@i < .@round_per_25)
			set .@speed,.@speed+25;
		else if (.@i < .@round_per_50)
			set .@speed,.@speed+100;
		else
			set .@speed,.@speed+250;
	}
	
	pcblockmove getcharid(3),0;
	set .@prize,$@LS_slot_item2[.@img];
	
	if (.@prize == 0)
	{
		mes .@npc$;
		mes "เสียใจด้วยท่านไม่ได้รับรางวัล";
		next;
		goto L2_Cancel;
	}
	
	mes .@npc$;
	mes "ยินดีด้วยท่านได้รับ "+getitemname(.@prize)+"";
	getitem .@prize, 1;
	close2;
	cutin "", 255;
	end;
	
L2_Cancel:
	mes .@npc$;
	mes "โอกาสหน้าเชิญใหม่";
	close2;
	cutin "", 255;
end;
OnInit:
waitingroom "Luck Spin Poring",0;
end;
}
