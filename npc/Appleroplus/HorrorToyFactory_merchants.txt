//===== rAthena Script =======================================
//= Horror Toy Factory merchants
//===== Description: =========================================
//= [Official Conversion]
//= Horror Toy Factory enchants / exchange in xmas
//===== Changelogs: ==========================================
//= 1.0 First version. Uses official script structure and text
//=     from iRO. [Capuche]
//============================================================

wonderworld,16,168,4	script	แลกของดัน Horror Toy#121	11063,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "น้ำหนักของคุณมากเกินไป";
		close;
	}
	if ((MaxWeight - Weight) < 10000) {
		mes "น้ำหนักคุณมากเกินไป โปรดลดแล้วกลับมาอีกครั้ง";
		close;
	}
	mes "[Billy The Golden Hands]";
	mes "ฮ่า ฮ่า คุณเอาเหรียญมาบ้างไหม? ฉันจะขายอะไรก็ได้ถ้าคุณมีเหรียญ";
	next;

	setarray .@xm_item[0],
		22534, 100,		// Closedmind_Box
		18848, 500,		// Lush_Rose
		19687, 1000,	// C_Lush_Rose
		19686, 500,		// C_SantaHairband
		19701, 500,		// C_Red_Bonnet
		13442, 1000,	// Old_Parasol
		11563, 7,		// Hot_Tee
		11564, 5,		// Sweet_Canape
		523,   1,		// Holy_Water
		12020, 1;		// Water_Of_Darkness

	.@s = ( select( "ยกเลิก", "Closed Mind Box (100 เหรียญ)", "Lush Rose (500 เหรียญ)", "C Lush Rose (1000 เหรียญ)", "C Santa Hairband (500 เหรียญ)", "C Red Bonnet (500 เหรียญ)", "Old Parasol (1000 เหรียญ)", "Hot Tea (7 เหรียญ)", "Sweet Canape (5 เหรียญ)", "Holy Water (1 เหรียญ)", "Water Of Darkness (1 เหรียญ)" ) -2 ) * 2;
	if (.@s == -2) {
		mes "[Billy The Golden Hands]";
		mes "คุณรู้สึกผิดหรืออะไร? เอาล่ะแล้ว กลับมาอีกครั้งได้ตลอดเวลาเมื่อคุณเปลี่ยนใจ";
		close;
	}
	if (countitem(7642) >= .@xm_item[.@s+1]) {
		mes "[Billy The Golden Hands]";
		mes "คุณต้องการ ^0000ff" + .@xm_item[.@s+1] + "^000000 Bloody Coins เพื่อซื้อ ^0000ff" + getitemname(.@xm_item[.@s]) + "^000000. คุณกำลังจะซื้อมันใช่ไหม?";
		next;
		if (select( "ยกเลิก", "ทำการซื้อ" ) == 1) {
			mes "[Billy The Golden Hands]";
			mes "Khaha, ถ้าคุณตัดสินใจที่จะซื้อจริงๆให้กลับมาอีกครั้ง";
			close;
		}
		if (countitem(7642) >= .@xm_item[.@s+1]) {
			delitem 7642,.@xm_item[.@s+1];// Bloody_Coin
			getitem .@xm_item[.@s], 1;
			mes "[Billy The Golden Hands]";
			mes "คุณจ่ายเงินแล้ว ฉันหวังว่าจะได้พบคุณอีกครั้ง!";
			close;
		}
	}
	mes "[Billy The Golden Hands]";
	mes "คุณต้องการ ^0000ff" + .@xm_item[.@s+1] + "^000000 Bloody Coins เพื่อซื้อ ^0000ff" + getitemname(.@xm_item[.@s]) + "^000000. คุณขาดไอเทม Bloody Coins";
	close;
}


wonderworld,19,168,4	script	แลกของดัน Horror Toy#1	11064,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "น้ำหนักของคุณมากเกินไป";
		close;
	}
	if ((MaxWeight - Weight) < 10000) {
		mes "น้ำหนักคุณมากเกินไป โปรดลดแล้วกลับมาอีกครั้ง";
		close;
	}
	mes "[Vagrant Cain]";
	mes "โอ้, ฉันแน่ใจว่าคุณมาที่นี่เพราะคุณไม่ชอบ ordinaries เพียงแค่คุยกับฉัน ถ้าคุณได้เตรียมค่าใช้จ่ายที่เหมาะสมและวัสดุที่เหมาะสม";
	next;
	.@s = select( "ไม่เป็นไร", "ฉันต้องการ Celine's Ribbon.", "ฉันกำลังมองหา Noble Cross.", "ฉันต้องการ Evilspirit Gloves." );
	switch(.@s) {
	case 1:
		mes "[Vagrant Cain]";
		mes "ใช่, ใช่. ทุกคนพูดเหมือนคุณในตอนแรกไว้พบกันใหม่";
		close;
	case 2:
		.@string$[0] = "โฮ้~ คุณกำลังมองหาหมวกที่ไม่ซ้ำกันมาก คุณมีความเฉลียวฉลาดมาก";
		.@string$[1] = "ฉันต้องการ ^0000ff1000 ^000000Bloody Coins และ ^0000ff+9 Lush Rose^000000 เพื่อ ^0000ffCeline's Ribbon^000000. คุณมีไอเทมเพียงพอหรือไม่?";
		.@string$[2] = "คุณควรเตรียม ^0000ff1000 ^000000Bloody Coins และ ^0000ff +9 Lush Rose^000000 เพื่อ ^0000ffCeline's Ribbon^000000. อย่าลืม!";
		.@string$[3] = "คุณกำลังจะแลกเปลี่ยนสินค้าใช่ไหม? ^ff0000ระดับการปรับแต่งผลกระทบและของสินค้าจะหายไปและคุณจะไม่ได้รับเงินคืน^000000. โปรดพิจารณาดีๆ";
		break;
	case 3:
		.@string$[0] = "โฮ้~ คุณกำลังมองหาหมวกที่ไม่ซ้ำกันมาก คุณมีความเฉลียวฉลาดมาก";
		.@string$[1] = "ฉันต้องการ ^0000ff2000 ^000000Bloody Coins และ ^0000ffGrand Cross (slot)^000000 เพื่อ ^0000ffNoble Cross^000000. คุณมีไอเทมเพียงพอหรือไม่?";
		.@string$[2] = "คุณควรเตรียม ^0000ff 2000 ^000000Bloody Coins และ ^0000ff Grand Cross (slot)^000000 เพื่อ ^0000ffNoble Cross^000000. อย่าลืม!";
		.@string$[3] = "คุณกำลังจะแลกเปลี่ยนสินค้าใช่ไหม? ^ff0000ระดับการปรับแต่งผลกระทบและของสินค้าจะหายไปและคุณจะไม่ได้รับเงินคืน^000000. โปรดพิจารณาดีๆ!";
		break;
	case 4:
		.@string$[0] = "โฮ้~ คุณกำลังมองหาหมวกที่ไม่ซ้ำกันมาก คุณมีความเฉลียวฉลาดมาก";
		.@string$[1] = "ฉันต้องการ ^0000ff1000 ^000000Bloody Coins, 1 ^0000ffHurt Mind, Kind Heart, และ Red Lantern ^000000สำหรับ ^0000ffEvilspirit Gloves^000000. คุณมีไอเทมเพียงพอหรือไม่?";
		.@string$[2] = "คุณควรเตรียม ^0000ff1000 ^000000Bloody Coins, 1 ^0000ffHurt Mind, Kind Heart, และ Red Lantern ^000000สำหรับ ^0000ffEvilspirit Gloves^000000. อย่าลืม!";
		.@string$[3] = "คุณกำลังจะแลกเปลี่ยนสินค้าใช่ไหม? ^ff0000ระดับการปรับแต่งผลกระทบและของสินค้าจะหายไปและคุณจะไม่ได้รับเงินคืน^000000. นอกเหนือจาก, ถ้าคุณมีสิ่งที่เหมือนกัน ฉันไม่รู้ว่าจะเลือกใครที่ คิดเกี่ยวกับเรื่องนี้";
		break;
	}
	mes "[Vagrant Cain]";
	mes .@string$[0];
	next;
	mes "[Vagrant Cain]";
	mes .@string$[1];
	next;
	if (select( "ฉันไม่มีไอเทมในตอนนี้", "ฉันมีไอเทมจะทำการและเปลี่ยน" ) == 1) {
		mes "[Vagrant Cain]";
		mes "กลับมาเมื่อคุณพร้อมและมีไอเทม";
		next;
		mes "[Vagrant Cain]";
		mes .@string$[2];
		close;
	}
	mes "[Vagrant Cain]";
	mes .@string$[3];
	next;
	if (select( "ฉันจะลองครั้งต่อไป", "แลกเปลี่ยนได้ทันที" ) == 1) {
		mes "[Vagrant Cain]";
		mes "โอเคร, คุณจะถูกกำหนด หวังว่าเราจะได้เจอกันครั้งต่อไป, ฮุ ฮุ.";
		close;
	}
	switch(.@s) {
	case 2:
		mes "[Vagrant Cain]";
		if (getequipid(EQI_HEAD_TOP) != 18848)// Lush_Rose
			mes "หมวกที่คุณใส่ไม่ใช่ Lush Rose. นำหมวกหรอืไอเทมที่ถูกต้องมาให้ฉัน, ฮ่า ฮ่า ฮ่า";
		else if (getequiprefinerycnt(EQI_HEAD_TOP) < 9)
			mes "หมวกที่คุณใส่ไม่ได้รับการตี +9 ขึ้นไป คุณไม่สามารถซื้อขายได้";
		else if (countitem(7642) < 1000)
			mes "สวัสดี, คุณมี Bloody Coins. คุณต้องรวบรวม 1000 เหรียญ";
		else {
			delitem 7642,1000;// Bloody_Coin
			delequip EQI_HEAD_TOP;
			getitem 18849,1;// Celines_Ribbon
			mes "ข้อตกลงนี้เสร็จสิ้นแล้ว ลองดูสิ";// custom
		}
		close;
	case 3:
		mes "[Vagrant Cain]";
		if (getequipid(EQI_HAND_R) != 1540)// Grand_Cross_
			mes "สิ่งที่อยู่ทางขวามือของคุณไม่ใช่ Grand Cross (slot). นำไอเทมที่ถูกต้องมาให้ฉัน, ฮ่า ฮ่า ฮ่า";
		else if (countitem(7642) < 2000)
			mes "สวัสดี, คุณมี of Bloody Coins. คุณต้องรวบรวม 2000 เหรียญ.";
		else {
			delitem 7642,2000;// Bloody_Coin
			delequip EQI_HAND_R;
			getitem 16029,1;// Noble_Cross
			mes "ข้อตกลงนี้เสร็จสิ้นแล้ว ลองดูสิ";// custom
		}
		close;
	case 4:
		mes "[Vagrant Cain]";
		if (countitem(7642) > 999 && countitem(2977) > 0 && countitem(2978) > 0 && countitem(2976) > 0) {
			delitem 7642,1000;// Bloody_Coin
			delitem 2977,1;// Hurt_Mind
			delitem 2978,1;// KindHeart
			delitem 2976,1;// Red_Lantern
			getitem 2980,1;// Evilspirit_Gloves
			mes "ข้อตกลงนี้เสร็จสิ้นแล้ว ลองดูสิ";// custom
		}
		else {
			mes "คุณไม่มีไอเทมไม่พอ";
			next;
			mes "[Vagrant Cain]";
			mes "คุณควรเตรียม ^0000ff1000 ^000000Bloody Coins, 1 ^0000ffHurt Mind, Kind Heart, และ Red Lantern ^000000เพื่อr ^0000ffEvilspirit Gloves^000000.";
		}
		close;
	}
}

wonderworld,22,168,4	script	ออฟของดัน Horror Toy#pa0829	11067,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "น้ำหนักของคุณมากเกินไป";
		close;
	}
	if ((MaxWeight - Weight) < 10000) {
		mes "น้ำหนักคุณมากเกินไป โปรดลดแล้วกลับมาอีกครั้ง";
		close;
	}
	mes "[Black Beard Joe]";
	mes "ฮ่า ฮ่า, ความโลภของมนุษย์มนุษย์เป็นสิ่งที่ไม่ต้องการตามธรรมชาติ คุณต้องการให้ฉันทำอะไร?";
	next;
	switch( select( "คุณสามารถทำอะไรให้ฉันได้บ้าง?", "ประดับ Enchant ของ Toy Factory", "อาวุธ Enchant ของ Toy Factory", "หมวก Enchant ของ Toy Factory", "รองเท้า Enchant ของ Toy Factory" ) ) {
	case 1:
		mes "[Black Beard Joe]";
		mes "ฉันจะทำอย่างไร? คุณเพิ่งมาหาฉัน Bloody Coins และ ฉันต้องการไอเทมเพิ่มเสริมพลัง";
		next;
		mes "[Black Beard Joe]";
		mes "ฉันต้องการ 15 Bloody Coins เพื่อฉันเสริมพลังให้คุณ 1 ช่อง โดยใช้ 15 Coins. แน่นอนว่า 3 คือค่าสูงสุด";
		next;
		mes "[Black Beard Joe]";
		mes "สวัสดี,มันเป็นธรรมชาติที่อัตราการทำลายจะได้รับสูงขึ้นเป็นระดับการอัพเกรดเพิ่มขึ้น. ฉันขอแนะนำให้คุณหยุดลองครั้งแรกหรือครั้งที่สอง,ฮ่า ฮ่า";
		close;
	case 2:
		.@eq_num = EQI_ACC_R;
		break;
	case 3:
		.@eq_num = EQI_HAND_R;
		break;
	case 4:
		.@eq_num = EQI_HEAD_TOP;
		break;
	case 5:
		.@eq_num = EQI_SHOES;
		break;
	}
	.@item_id = getequipid(.@eq_num);
	.@refine = getequiprefinerycnt(.@eq_num);
	setarray .@card[0],
		getequipcardid(.@eq_num,0),
		getequipcardid(.@eq_num,1),
		getequipcardid(.@eq_num,2),
		getequipcardid(.@eq_num,3);

	switch(.@item_id) {
	case 2976:	// Red_Lantern
	case 2977:	// Hurt_Mind
	case 2978:	// KindHeart
	case 2980:	// Evilspirit_Gloves
		.@sc_type = 1;// acc
		break;
	case 2486:	// Shadow_Walk_
	case 18848:	// Lush_Rose
	case 18849:	// Celines_Ribbon
		.@sc_type = 2;// armor
		break;
	case 13442:	// Old_Parasol
		.@sc_type = 3;// weapon
		break;
	case 16029:	// Noble_Cross
		mes "[Black Beard Joe]";// custom
		mes "โอ้, คุณมี ^0000ffNoble Cross^000000! นี้คือสิ่งที่ต้องเสริมพลังมัน.";
		next;
		mes "[Black Beard Joe]";
		mes "ฉันพยายามหลายครั้ง แต่ไม่มีโชค";
		next;
		mes "[Black Beard Joe]";
		mes "ถ้าคุณต้องการใช้อาวุธนี้ให้ค้นหา ^0000ffMayomayo^000000 ในเมือง Malangdo เขาควรจะสามารถทำมันได้";
		close;
	default:
		break;
	}
	switch(.@eq_num) {
	case EQI_ACC_R:
		mes "[Black Beard Joe]";
		mes "คุณสามารถเลือกหนึ่งในสองประเภทและค่าธรรมเนียมสำหรับแต่ละครั้งคือ 15 เหรียญ Bloody Coin. ฉันจะทำให้ถูกต้องก่อนเสริมพลัง";
		next;
		.@enchant_type = select( "ยกเลิก", "สุ่ม STR/AGI/DEX", "สุ่ม INT/VIT/DEX" ) - 1;// enchant_type [1-2]
		mes "[Black Beard Joe]";
		if (.@enchant_type == 0) {
			mes "โอเคร, มาอีกครั้งเมื่อคุณยินดีที่จะเสริมพลัง, ฮ่า ฮ่า.";
			close;
		}
		if (.@sc_type < 1) {
			mes "อุปกรณ์นี้ไม่ใช่ของดัน Toy Factory. ฉันสามารถเสริมพลังไอเทมจาก Toy Factory เท่านั้น";
			close;
		}
		if (.@card[3] == 0) {
			.@slot_num = 3;
			mes "โอเคร, ลองมาดูกันก่อน โดยปกติแล้ว ไอเทมจะไม่แตกโดยโอกาสสำเร็จ 95%";
			.@string$ = "You blow hot and cold, this fickle friend.!";
		}
		else if (.@card[2] == 0) {
			.@slot_num = 2;
			mes "โฮ้~ เสริมพลังตอนนี้มันกลายเป็นอันตราย โอกาสสำเร็จ 70%. คุณต้องการดำเนินการ? อุปกรณ์ทั้งหมดจะหายถ้าล้มเหลว.";// custom
			.@string$ = "You blow hot and cold, this fickle friend.!";
		}
		else if (.@card[1] == 0) {
			.@slot_num = 1;
			mes "นี้เป็นการเสริมพลังครั้งสุดท้าย. คุณจะไปต่อไหม? ณ ขณะนี้, ^ff0000โอกาสสำเร็จ 50%^000000. แน่นอน, มันจะดีมากถ้ามันประสบความสำเร็จ... ฮึ๋ม, ฉันจะปฏิบัติตามการตัดสินใจของคุณ";// custom
			.@string$ = "Wise! The third enchantment is too greedy.";// custom translation
		}
		else {
			mes "โฮ้~ ระดับการเสริมพลังถึงจุดสูงสุดแล้ว. มันเป็นไปไม่ได้ที่จะเสริมพลังอีก";// custom
			close;
		}
		break;
	case EQI_HAND_R:
		mes "[Black Beard Joe]";
		if (.@sc_type < 1) {
			mes "อุปกรณ์นี้ไม่ใช่ของดัน Toy Factory. ฉันสามารถเสริมพลังไอเทมจาก Toy Factory เท่านั้น";
			close;
		}
		.@enchant_type = 3;
		.@slot_num = 3;
		mes "ฉันสามารถเสริมพลังให้กับอาวุธของคุณ. ไม่มีความล้มเหลว, ดังนั้นคุณจึงสามารถคิดว่ามันเป็นโบนัสพิเศษ, ฮ่า ฮ่า";
		.@string$ = "You blow hot and cold, this fickle friend.!";
		break;
	case EQI_HEAD_TOP:
	case EQI_SHOES:
		mes "[Black Beard Joe]";
		if (.@sc_type < 1) {
			mes "อุปกรณ์นี้ไม่ใช่ของดัน Toy Factory. ฉันสามารถเสริมพลังไอเทมจาก Toy Factory เท่านั้น";
			close;
		}
		if (.@card[3] == 0) {
			.@slot_num = 3;
			.@enchant_type = 4;
			mes "ดี. ลองเริ่มต้นเสริมพลัง. ฉันจะเพิ่มพลังหนึ่งในผลการต่อสู้พิเศษ.  คุณไม่ต้องกลัวเพราะไม่มีความล้มเหลว, ฮ่า ฮ่า";
		}
		else if (.@card[2] == 0) {
			.@slot_num = 2;
			.@enchant_type = 5;
			mes "การเสริมพลังขั้นที่ 2. ฉันจะ buff หนึ่งใน State นี้ คุณไม่ต้องกลัวเพราะไม่มีความล้มเหลว, ฮ่า ฮ่า";// custom
		}
		else {
			mes "โฮ้~ ระดับการเสริมพลังถึงจุดสูงสุดแล้ว. มันเป็นไปไม่ได้ที่จะเสริมพลังอีก";// custom
			close;
		}
		.@string$ = "You blow hot and cold, this fickle friend.!";
		break;
	default:
		mes "[Black Beard Joe]";
		mes "ฉันไม่เสริมพลังนี้";// custom
		close;
	}
	next;
	if (select( "I will com back later", "Please do it" ) == 1) {
		mes "[Black Beard Joe]";
		mes .@string$;
		close;
	}
	mes "[Black Beard Joe]";
	if (.@slot_num == 3)
		setarray .@range[0],550,1050;
	else if (.@slot_num == 2)
		setarray .@range[0],450,1125;
	else if (.@slot_num == 1)
		setarray .@range[0],1,1161;
	else {
		mes "มีปัญหา, โปรดกลับมาอีกครั้ง!";// custom
		close;
	}
	.@enchant = 9;
	switch(.@enchant_type) {
	case 1:
		.@r = rand(.@range[0],.@range[1]);
			 if (.@r < 601)  .@enchant = 0;
		else if (.@r < 701)  .@enchant = 4700;	// Strength1
		else if (.@r < 801)  .@enchant = 4730;	// Agility1
		else if (.@r < 901)  .@enchant = 4720;	// Dexterity1
		else if (.@r < 951)  .@enchant = 4701;	// Strength2
		else if (.@r < 1001) .@enchant = 4731;	// Agility2
		else if (.@r < 1051) .@enchant = 4721;	// Dexterity2
		else if (.@r < 1076) .@enchant = 4702;	// Strength3
		else if (.@r < 1101) .@enchant = 4732;	// Agility3
		else if (.@r < 1126) .@enchant = 4722;	// Dexterity3
		else if (.@r < 1138) .@enchant = 4703;	// Strength4
		else if (.@r < 1150) .@enchant = 4733;	// Agility4
		else if (.@r < 1162) .@enchant = 4723;	// Dexterity4
		else .@enchant = 9;
		break;
	case 2:
		.@r = rand(.@range[0],.@range[1]);
			 if (.@r < 601)  .@enchant = 0;
		else if (.@r < 701)  .@enchant = 4710;	// Inteligence1
		else if (.@r < 801)  .@enchant = 4740;	// Vitality1
		else if (.@r < 901)  .@enchant = 4720;	// Dexterity1
		else if (.@r < 951)  .@enchant = 4711;	// Inteligence2
		else if (.@r < 1001) .@enchant = 4741;	// Vitality2
		else if (.@r < 1051) .@enchant = 4721;	// Dexterity2
		else if (.@r < 1076) .@enchant = 4712;	// Inteligence3
		else if (.@r < 1101) .@enchant = 4742;	// Vitality3
		else if (.@r < 1126) .@enchant = 4722;	// Dexterity3
		else if (.@r < 1138) .@enchant = 4713;	// Inteligence4
		else if (.@r < 1150) .@enchant = 4743;	// Vitality4
		else if (.@r < 1162) .@enchant = 4723;	// Dexterity4
		else .@enchant = 9;
		break;
	case 3:
		.@r = rand(1,2555);
			 if (.@r < 301)  .@enchant = 4820;	// Fighting_Spirit5
		else if (.@r < 501)  .@enchant = 4821;	// Fighting_Spirit6
		else if (.@r < 601)  .@enchant = 4822;	// Fighting_Spirit7
		else if (.@r < 651)  .@enchant = 4823;	// Fighting_Spirit8
		else if (.@r < 676)  .@enchant = 4824;	// Fighting_Spirit9
		else if (.@r < 686)  .@enchant = 4825;	// Fighting_Spirit10
		else if (.@r < 986)  .@enchant = 4816;	// Sharp3
		else if (.@r < 1086) .@enchant = 4843;	// Sharp4
		else if (.@r < 1096) .@enchant = 4844;	// Sharp5
		else if (.@r < 1396) .@enchant = 4760;	// Matk1
		else if (.@r < 1496) .@enchant = 4761;	// Matk2
		else if (.@r < 1506) .@enchant = 4806;	// Matk3
		else if (.@r < 1806) .@enchant = 4872;	// Attack_Delay_2
		else if (.@r < 2006) .@enchant = 4873;	// Attack_Delay_3
		else if (.@r < 2106) .@enchant = 4881;	// Attack_Delay_4
		else if (.@r < 2116) .@enchant = 4807;	// Atk_Speed1
		else if (.@r < 2416) .@enchant = 4827;	// Spell6
		else if (.@r < 2516) .@enchant = 4828;	// Spell7
		else if (.@r < 2546) .@enchant = 4829;	// Spell8
		else if (.@r < 2556) .@enchant = 4830;	// Spell9
		else .@enchant = 9;
		break;
	case 4:
		.@r = rand(1,1950);
			 if (.@r < 301)  .@enchant = 4810;	// Fighting_Spirit2
		else if (.@r < 401)  .@enchant = 4809;	// Fighting_Spirit3
		else if (.@r < 451)  .@enchant = 4808;	// Fighting_Spirit4
		else if (.@r < 471)  .@enchant = 4820;	// Fighting_Spirit5
		else if (.@r < 771)  .@enchant = 4818;	// Sharp1
		else if (.@r < 871)  .@enchant = 4817;	// Sharp2
		else if (.@r < 891)  .@enchant = 4816;	// Sharp3
		else if (.@r < 991)  .@enchant = 4760;	// Matk1
		else if (.@r < 1011) .@enchant = 4761;	// Matk2
		else if (.@r < 1311) .@enchant = 4869;	// Attack_Delay_1
		else if (.@r < 1411) .@enchant = 4872;	// Attack_Delay_2
		else if (.@r < 1461) .@enchant = 4873;	// Attack_Delay_3
		else if (.@r < 1481) .@enchant = 4881;	// Attack_Delay_4
		else if (.@r < 1781) .@enchant = 4813;	// Spell3
		else if (.@r < 1881) .@enchant = 4812;	// Spell4
		else if (.@r < 1931) .@enchant = 4826;	// Spell5
		else if (.@r < 1951) .@enchant = 4827;	// Spell6
		else .@enchant = 9;
		break;
	case 5:
		.@r = rand(1,1970);
			 if (.@r < 301)  .@enchant = 4700;	// Strength1
		else if (.@r < 401)  .@enchant = 4701;	// Strength2
		else if (.@r < 451)  .@enchant = 4702;	// Strength3
		else if (.@r < 471)  .@enchant = 4730;	// Agility1
		else if (.@r < 771)  .@enchant = 4731;	// Agility2
		else if (.@r < 871)  .@enchant = 4732;	// Agility3
		else if (.@r < 891)  .@enchant = 4710;	// Inteligence1
		else if (.@r < 991)  .@enchant = 4711;	// Inteligence2
		else if (.@r < 1011) .@enchant = 4712;	// Inteligence3
		else if (.@r < 1311) .@enchant = 4720;	// Dexterity1
		else if (.@r < 1411) .@enchant = 4721;	// Dexterity2
		else if (.@r < 1461) .@enchant = 4722;	// Dexterity3
		else if (.@r < 1481) .@enchant = 4740;	// Vitality1
		else if (.@r < 1781) .@enchant = 4741;	// Vitality2
		else if (.@r < 1881) .@enchant = 4742;	// Vitality3
		else if (.@r < 1931) .@enchant = 4750;	// Luck1
		else if (.@r < 1951) .@enchant = 4751;	// Luck2
		else if (.@r < 1971) .@enchant = 4752;	// Luck3
		else .@enchant = 9;
		break;
	default:
		break;
	}
	if (.@enchant == 9) {
		mes "มีปัญหา, โปรดกลับมาอีกครั้ง!";// custom
		close;
	}
	if (.@card[3] == 0 && .@sc_type < 4) 
		.@card[3] = .@enchant;
	else if (.@card[2] == 0 && .@sc_type < 3)
		.@card[2] = .@enchant;
	else if (.@card[1] == 0 && .@sc_type < 2)
		.@card[1] = .@enchant;
	else {
		mes "ฉันไม่รู้ว่านี่คืออะไร, แต่ฉันคิดว่านี่ไม่ใช่สิ่งที่ฉันสามารถจัดการได้";// custom
		close;
	}
	if (countitem(7642) > 14) {// Bloody_Coin
		delitem 7642,15;// Bloody_Coin
		delequip .@eq_num;
		if (.@enchant == 0) {
			mes "อุ๊ย! น่าสงสารคุณ, การเสริมพลังมีความล้มเหลวและอุปกรณ์ของคุณแตก, กรี๊ซ!";
			close2;
			specialeffect2 EF_LORD;
			end;
		}
		getitem2 .@item_id,1,1,.@refine,0,.@card[0],.@card[1],.@card[2],.@card[3];
		mes "ฮึ๋ม.. มันประสบความสำเร็จลองดูสิ";
		specialeffect2 EF_REPAIRWEAPON;
		close;
	}
	mes "ฮึ๋ม.. ยังไงซะ, คุณต้องการ Bloody Coins. คุณไม่สามารถแม้แต่จะจ่ายค่าธรรมเนียมได้";
	close;
}
